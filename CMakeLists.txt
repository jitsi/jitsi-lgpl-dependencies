cmake_minimum_required (VERSION 3.10)
project(jitsi-lgpl-dependencies)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake")
find_package(PkgConfig REQUIRED)

if(WIN32)
    set(DEFAULT_USE_SYSTEM_FFMPEG OFF)
    set(DEFAULT_USE_SYSTEM_LAME OFF)
    set(DEFAULT_USE_SYSTEM_OPENH264 OFF)
elseif(APPLE)
    set(DEFAULT_USE_SYSTEM_FFMPEG OFF)
    set(DEFAULT_USE_SYSTEM_LAME OFF)
    set(DEFAULT_USE_SYSTEM_OPENH264 OFF)
    set(CMAKE_OSX_DEPLOYMENT_TARGET "10.12" CACHE STRING "Minimum OS X deployment version")
else()
    set(DEFAULT_USE_SYSTEM_FFMPEG ON)
    set(DEFAULT_USE_SYSTEM_LAME ON)
    set(DEFAULT_USE_SYSTEM_OPENH264 OFF)
endif()

option(USE_SYSTEM_FFMPEG "Use the system provided FFmpeg?" ${DEFAULT_USE_SYSTEM_FFMPEG})
option(USE_SYSTEM_LAME "Use the system Lame? Only relevant if not using the system FFmpeg" ${DEFAULT_USE_SYSTEM_LAME})
option(USE_SYSTEM_OPENH264 "Use the system OpenH264? Only relevant if not using the system FFmpeg" ${DEFAULT_USE_SYSTEM_OPENH264})

include(GNUInstallDirs)
add_subdirectory(thirdparty)
add_subdirectory(cmake/jnffmpeg)
add_subdirectory(src/main/c/g722)
if(WIN32 OR APPLE)
    add_subdirectory(cmake/jnffmpeg_oh264)
    add_dependencies(jnffmpeg_oh264 ffmpeg_OH264)
endif()
